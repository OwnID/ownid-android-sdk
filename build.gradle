plugins {
    id "com.android.application" version "8.2.2" apply false
    id "com.android.library" version "8.2.2" apply false
    id "org.jetbrains.kotlin.android" version "1.9.22" apply false
    id "org.jetbrains.kotlin.plugin.serialization" version "1.9.22" apply false
    id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
    id "org.jetbrains.kotlinx.binary-compatibility-validator" version "0.13.2" apply false
}

group = "com.ownid.android-sdk"
version = "3.0.1"

ext {
    minSdkVersion = 23
    targetSdkVersion = 34
    compileSdkVersion = targetSdkVersion
    buildToolsVersion = "34.0.0"

    demoVersionCode = 30000
    demoVersionName = version

    coreVersion = version
    composeVersion = version
    firebaseVersion = version
    gigyaVersion = version
    cognitoVersion = version

    signingKeyId = ""
    signingKey = ""
    signingPassword = ""
    sonatypeStagingProfileId = ""
    ossrhUsername = ""
    ossrhPassword = ""
}

Properties localProps = new Properties()
File localProperties = new File(rootProject.rootDir, "local.properties")
if (localProperties.exists() && localProperties.isFile()) {
    localProperties.withInputStream { localProps.load(it) }

    rootProject.ext["signingKeyId"] = localProps.getProperty("signingKeyId")
    rootProject.ext["signingKey"] = localProps.getProperty("signingKey")
    rootProject.ext["signingPassword"] = localProps.getProperty("signingPassword")
    rootProject.ext["sonatypeStagingProfileId"] = localProps.getProperty("sonatypeStagingProfileId")
    rootProject.ext["ossrhUsername"] = localProps.getProperty("ossrhUsername")
    rootProject.ext["ossrhPassword"] = localProps.getProperty("ossrhPassword")
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            stagingProfileId = rootProject.ext.sonatypeStagingProfileId
            username = project.ext.ossrhUsername
            password = project.ext.ossrhPassword
        }
    }
}