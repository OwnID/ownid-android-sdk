apply plugin: "maven-publish"
apply plugin: "signing"

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = project.ext.groupId
                artifactId = project.ext.artifactId
                version = project.ext.versionName

                from components.release
                artifact sourceJar

                pom {
                    name = project.ext.name
                    description = "Enable passwordless authentication to your existing flows within minutes"
                    url = "https://ownid.com"

                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }

                    developers {
                        developer {
                            id = "dkrivoruchko"
                            name = "Dmitriy Krivoruchko"
                            email = "dmitriy@ownid.com"
                        }
                    }

                    scm {
                        url = "https://github.com/OwnID/ownid-android-sdk"
                    }
                }
            }
        }

        repositories {
            maven {
                name = "sonatype"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = rootProject.ext.ossrhUsername
                    password = rootProject.ext.ossrhPassword
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signingKeyId"],
            rootProject.ext["signingKey"],
            rootProject.ext["signingPassword"]
    )
    sign publishing.publications
}