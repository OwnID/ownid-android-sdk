plugins {
    id("com.android.library")
    id("org.jetbrains.kotlin.android")
    id("binary-compatibility-validator")
}

ext {
    mavenName = "OwnID Gigya Android SDK"
    mavenGroupId = "com.ownid.android-sdk"
    mavenArtifactId = "gigya"
    mavenVersionName = rootProject.ext.gigyaVersion
}

apply from: rootProject.file("publishing.gradle")

android {
    namespace = "com.ownid.sdk.gigya"
    compileSdk = rootProject.ext.compileSdkVersion
    buildToolsVersion = rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdk = rootProject.ext.minSdkVersion
        targetSdk = rootProject.ext.targetSdkVersion

        gradle.projectsEvaluated { preBuild.dependsOn(setVersionProperties) }
    }

    publishing {
        singleVariant("release") { withSourcesJar() }
    }

    testOptions {
        unitTests { includeAndroidResources = true }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += "-Xexplicit-api=strict"
    }

    lint {
        enable += "Interoperability"
    }

    apiValidation {
        nonPublicMarkers += ["kotlin.PublishedApi", "com.ownid.sdk.InternalOwnIdAPI"]
    }
}

dependencies {
    api(project(":sdk-core"))

    compileOnly("com.sap.oss.gigya-android-sdk:sdk-core:7.0.9")

    testImplementation("com.sap.oss.gigya-android-sdk:sdk-core:7.0.9")
    testImplementation("com.google.code.gson:gson:2.10.1")
    testImplementation("junit:junit:4.13.2")
    testImplementation("androidx.test.ext:truth:1.5.0")
    testImplementation("org.robolectric:robolectric:4.12")
    testImplementation("org.awaitility:awaitility-kotlin:4.2.1")
    testImplementation("io.mockk:mockk:1.13.10")
}

def propertiesFileName = "src/main/assets/com/ownid/sdk/gigya.properties"
clean { delete propertiesFileName }
tasks.register("setVersionProperties", WriteProperties) {
    outputFile file(propertiesFileName)
    property "name", "OwnIDGigya"
    property "version", rootProject.ext.gigyaVersion
}