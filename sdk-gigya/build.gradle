plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "binary-compatibility-validator"
}

ext {
    name = "OwnID Gigya Android SDK"
    groupId = "com.ownid.android-sdk"
    artifactId = "gigya"
    versionName = "2.2.1"
}

apply from: rootProject.file("publishing.gradle")

android {
    namespace "com.ownid.sdk.gigya"
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        gradle.projectsEvaluated {
            preBuild.dependsOn(setVersionProperties)
        }
    }

    testOptions {
        unitTests { includeAndroidResources = true }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += ["-module-name", "com.ownid.sdk.gigya"]
        freeCompilerArgs += "-Xexplicit-api=strict"
    }

    lint {
        enable "Interoperability"
    }

    apiValidation {
        nonPublicMarkers += ["kotlin.PublishedApi", "com.ownid.sdk.InternalOwnIdAPI"]
    }
}

dependencies {
    api project(":sdk-core")
    //noinspection GradleDependency
    api "androidx.annotation:annotation:1.3.0"
    api "com.github.SAP.gigya-android-sdk:sdk-core:core-v6.1.0"
    api "com.google.code.gson:gson:2.10"

    testImplementation "junit:junit:4.13.2"
    testImplementation "androidx.test.ext:truth:1.5.0"
    testImplementation "org.robolectric:robolectric:4.9"
    testImplementation "org.awaitility:awaitility-kotlin:4.2.0"
    testImplementation "io.mockk:mockk:1.13.2"
}

def propertiesFileName = "src/main/assets/com/ownid/sdk/gigya.properties"

task setVersionProperties(type: WriteProperties) {
    outputFile file(propertiesFileName)
    property "name", "OwnIDGigya"
    property "version", project.ext.versionName
}

clean { delete propertiesFileName }