plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "binary-compatibility-validator"
}

ext {
    name = "OwnID Core Android SDK"
    groupId = "com.ownid.android-sdk"
    artifactId = "core"
    versionName = "2.2.1"
}

apply from: rootProject.file("publishing.gradle")

android {
    namespace "com.ownid.sdk"
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        gradle.projectsEvaluated {
            preBuild.dependsOn(setVersionProperties)
        }

        consumerProguardFiles "consumer-rules.pro"
    }

    testOptions {
        unitTests { includeAndroidResources = true }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += ["-module-name", "com.ownid.sdk.core"]
        freeCompilerArgs += "-Xexplicit-api=strict"
    }

    lint {
        enable "Interoperability"
    }

    apiValidation {
        nonPublicMarkers += ["kotlin.PublishedApi", "com.ownid.sdk.InternalOwnIdAPI"]
    }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib:1.8.21"

    //noinspection GradleDependency,KtxExtensionAvailable
    api "androidx.lifecycle:lifecycle-runtime:2.4.1"
    //noinspection GradleDependency,KtxExtensionAvailable
    api "androidx.lifecycle:lifecycle-viewmodel:2.4.1"
    //noinspection GradleDependency,KtxExtensionAvailable
    api "androidx.lifecycle:lifecycle-livedata-core:2.4.1"
    //noinspection GradleDependency
    api "androidx.appcompat:appcompat:1.3.1" //DO NOT update version - this is the minimum version required
    api "androidx.annotation:annotation-experimental:1.3.0"
    api "androidx.browser:browser:1.4.0"
    api "com.squareup.okio:okio:3.2.0"
    api "com.squareup.okhttp3:okhttp:4.10.0"

    testImplementation "junit:junit:4.13.2"
    testImplementation "androidx.test.ext:truth:1.5.0"
    testImplementation "org.robolectric:robolectric:4.9"
    testImplementation "com.squareup.okhttp3:mockwebserver:4.10.0"
    testImplementation "org.awaitility:awaitility-kotlin:4.2.0"
    testImplementation "io.mockk:mockk:1.13.2"
}

def propertiesFileName = "src/main/assets/com/ownid/sdk/core.properties"

task setVersionProperties(type: WriteProperties) {
    outputFile file(propertiesFileName)
    property "name", "OwnIDCore"
    property "version", project.ext.versionName
}

clean { delete propertiesFileName }