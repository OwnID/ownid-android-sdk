plugins {
    id("com.android.library")
    id("org.jetbrains.kotlin.android")
    id("binary-compatibility-validator")
}

ext {
    mavenName = "OwnID Core Android SDK"
    mavenGroupId = "com.ownid.android-sdk"
    mavenArtifactId = "core"
    mavenVersionName = rootProject.ext.coreVersion
}

apply from: rootProject.file("publishing.gradle")

android {
    namespace = "com.ownid.sdk"
    compileSdk = rootProject.ext.compileSdkVersion
    buildToolsVersion = rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdk = rootProject.ext.minSdkVersion
        targetSdk = rootProject.ext.targetSdkVersion

        gradle.projectsEvaluated { preBuild.dependsOn(setVersionProperties) }

        consumerProguardFiles "consumer-rules.pro"
    }

    publishing {
        singleVariant("release") { withSourcesJar() }
    }

    testOptions {
        unitTests { includeAndroidResources = true }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += "-Xexplicit-api=strict"
    }

    lint {
        enable += "Interoperability"
    }

    apiValidation {
        nonPublicMarkers += ["kotlin.PublishedApi", "com.ownid.sdk.InternalOwnIdAPI"]
    }
}

dependencies {
    api("org.jetbrains.kotlin:kotlin-stdlib:1.8.22")
    api("com.google.android.material:material:1.11.0")
    api("androidx.fragment:fragment:1.6.2")
    api("androidx.constraintlayout:constraintlayout:2.1.4")
    api("androidx.browser:browser:1.7.0")
    api("androidx.webkit:webkit:1.9.0")
    api("com.squareup.okio:okio:3.4.0")
    api("com.squareup.okhttp3:okhttp:4.11.0")

    api("androidx.credentials:credentials:1.2.0")
    api("androidx.credentials:credentials-play-services-auth:1.2.0")

    testImplementation("junit:junit:4.13.2")
    testImplementation("androidx.test.ext:truth:1.5.0")
    testImplementation("org.robolectric:robolectric:4.11.1")
    testImplementation("com.squareup.okhttp3:mockwebserver:4.12.0")
    testImplementation("org.awaitility:awaitility-kotlin:4.2.0")
    testImplementation("io.mockk:mockk:1.13.9")
    testImplementation("org.json:json:20240205")
}

def propertiesFileName = "src/main/assets/com/ownid/sdk/core.properties"
clean { delete propertiesFileName }
tasks.register("setVersionProperties", WriteProperties) {
    outputFile file(propertiesFileName)
    property "name", "OwnIDCore"
    property "version", rootProject.ext.coreVersion
}
