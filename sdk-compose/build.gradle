plugins {
    id("com.android.library")
    id("org.jetbrains.kotlin.android")
    id("binary-compatibility-validator")
}

ext {
    mavenName = "OwnID Compose Android SDK"
    mavenGroupId = "com.ownid.android-sdk"
    mavenArtifactId = "compose"
    mavenVersionName = rootProject.ext.composeVersion
}

apply from: rootProject.file("publishing.gradle")

android {
    namespace = "com.ownid.sdk.compose"
    compileSdk = rootProject.ext.compileSdkVersion
    buildToolsVersion = rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdk = rootProject.ext.minSdkVersion
        targetSdk = rootProject.ext.targetSdkVersion

        gradle.projectsEvaluated { preBuild.dependsOn(setVersionProperties) }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += "-Xexplicit-api=strict"
    }

    buildFeatures {
        compose = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.8"
    }

    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }

    lint {
        enable += "Interoperability"
    }

    apiValidation {
        nonPublicMarkers += ["kotlin.PublishedApi", "com.ownid.sdk.InternalOwnIdAPI"]
    }
}

dependencies {
    api(project(":sdk-core"))

    api("androidx.compose.ui:ui:1.5.4")
    api("androidx.lifecycle:lifecycle-viewmodel-compose:2.6.2")
}

def propertiesFileName = "src/main/assets/com/ownid/sdk/compose.properties"
clean { delete propertiesFileName }
tasks.register("setVersionProperties", WriteProperties) {
    outputFile file(propertiesFileName)
    property "name", "OwnIDCompose"
    property "version", rootProject.ext.composeVersion
}