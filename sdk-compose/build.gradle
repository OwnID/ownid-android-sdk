plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "binary-compatibility-validator"
}

ext {
    name = "OwnID Compose Android SDK"
    groupId = "com.ownid.android-sdk"
    artifactId = "compose"
    versionName = "2.2.0"
}

apply from: rootProject.file("publishing.gradle")

android {
    namespace "com.ownid.sdk.compose"
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        gradle.projectsEvaluated {
            preBuild.dependsOn(setVersionProperties)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += "-Xexplicit-api=strict"
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion "1.4.7"
    }

    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }

    lint {
        enable "Interoperability"
    }

    apiValidation {
        nonPublicMarkers += ["kotlin.PublishedApi", "com.ownid.sdk.InternalOwnIdAPI"]
    }
}

dependencies {
    api project(":sdk-core")

    api "androidx.compose.ui:ui:1.4.3"
    api "androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1"
}


def propertiesFileName = "src/main/assets/com/ownid/sdk/compose.properties"

clean { delete propertiesFileName }
task setVersionProperties(type: WriteProperties) {
    outputFile file(propertiesFileName)
    property "name", "OwnIDCompose"
    property "version", project.ext.versionName
}

clean { delete propertiesFileName }